import java.time.Duration

buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

group = 'com.wexalian.jmagnet'
version = '0.0.1-SNAPSHOT'


repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation 'com.wexalian.nullability:nullability:0.0.5'
    implementation 'com.wexalian.common:common:0.0.26-SNAPSHOT'

    implementation 'com.google.code.gson:gson:2.10.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.9.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }

    withSourcesJar()
    withJavadocJar()
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'jmagnet'
            from components.java

            pom {
                name = 'jmagnet'
                packaging = 'jar'
                description = 'A Java library to aid in parsing/downloading torrents from magnet links, designed for use with tv shows'
                url = 'https://www.github.com/Wexalian/jmagnet'
                licenses {
                    license {
                        name = 'The Unlicense'
                        url = 'https://unlicense.org/'
                    }
                }
                developers {
                    developer {
                        id = 'Wexalian'
                        name = 'Lars Gerrits'
                        email = 'larsgerrits99@gmail.com'
                        organization = 'wexalian'
                        organizationUrl = 'https://www.wexalian.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Wexalian/jmagnet.git'
                    developerConnection = 'scm:git:ssh://github.com/Wexalian/jmagnet.git'
                    url = 'https://www.github.com/Wexalian/jmagnet'
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl = uri("https://s01.oss.sonatype.org/service/local/")
            snapshotRepositoryUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
            username = deployRepoUsername
            password = deployRepoPassword
        }
    }
    transitionCheckOptions {
        maxRetries.set(40) //40 tries of 3 seconds = 2 minutes
        delayBetween.set(Duration.ofMillis(3000))
    }
}

signing {
    sign publishing.publications.mavenJava
}